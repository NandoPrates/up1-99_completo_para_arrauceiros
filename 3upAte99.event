automacro todobugadão {
	InMapRegex /^(?!new_\d-\d|moc_prydb1)$/
	JobID 0
	exclusive 1
	JobLevel = 10
	timeout 100
	call {
		[
		log WTF?
		log Deveria ser gatuno agora, mas nem isso eu sou
		log tô todo bugado
		log vamo virar gatuno, tá na hora
		]
		do move moc_prydb1 &rand(44,46) &rand(127,129)
	}
}

automacro upAteLv12Aprendiz {
	ConfigKeyNot quest_eden em_curso
	ConfigKeyNot quest_eden terminando
	ConfigKeyNot lockMap pay_fild01
	ConfigKeyNot In_saveMap_sequence true
	JobLevel != 10
	JobID 0
	exclusive 1
	timeout 30
	InMapRegex /^(?!new_\d-\d|moc_prydb1)$/
	call {
		[
		log pelo visto, ainda to no lvl de Classe $JobLevelLast ainda...
		log o que aconteceu?
		log a que ponto chegamos?
		log nuss
		log Tentando resolver isso...
		]
		call configurarMapa "payon" "pay_fild01"
	}
}

automacro upAteLv12 {
	ConfigKeyNot quest_eden em_curso
	ConfigKeyNot quest_eden terminando
	ConfigKeyNot lockMap pay_fild01
	ConfigKeyNot In_saveMap_sequence true
	BaseLevel 1..11
	NotInMap new_1-3, new_2-3, new_3-3, new_4-3, new_5-3
	exclusive 1
	JobID 6 #gatuno
	priority 5
	timeout 30
	call configurarMapa "payon" "pay_fild01"
}

automacro upAteLv26 {
	ConfigKeyNot quest_eden em_curso
	ConfigKeyNot quest_eden terminando
	ConfigKeyNot lockMap pay_fild08
	ConfigKeyNot In_saveMap_sequence true
	BaseLevel 12..25
	exclusive 1
	JobID 6 #gatuno
	priority 5
	timeout 30
	call {
		do conf buyAuto_0_npc payon_in01 5 49
		do iconf Erva Azul 7 1 0
		call configurarMapa "payon" "pay_fild08"
	}
}

automacro upAteLv40 {
	ConfigKeyNot quest_eden em_curso
	ConfigKeyNot quest_eden terminando
	ConfigKeyNot lockMap pay_dun00
	ConfigKeyNot In_saveMap_sequence true
	BaseLevel 26..40
	exclusive 1
	JobID 6 #gatuno
	priority 5
	timeout 30
	call {
		do iconf Unha Apodrecida 10 1 0
		do iconf Osso 10 1 0
		do iconf Mandíbula Horrenda 10 1 0
		do mconf 1084 0 0 0 #cugomelo negro, troço inútil
		do conf buyAuto_0_npc payon_in01 5 49
		call configurarMapa "payon" "pay_dun00"
	}
}

automacro upAteLv46 {
	BaseLevel 41..45
	ConfigKeyNot quest_eden em_curso
	ConfigKeyNot quest_eden terminando
	ConfigKeyNot lockMap pay_fild09
	ConfigKeyNot In_saveMap_sequence true
	exclusive 1
	JobID 6 #gatuno
	priority 5
	timeout 30
	call configurarMapa "payon" "pay_fild09"
}

automacro upAteLv54_pegandoErvaAzul {
	BaseLevel 46..53
	ConfigKeyNot quest_eden em_curso
	ConfigKeyNot quest_eden terminando
	ConfigKeyNot lockMap cmd_fild02
	ConfigKeyNot In_saveMap_sequence true
	ConfigKeyNot virarArruaceiro true
	InInventory "Erva Azul" < 7
	exclusive 1
	JobID 6 #gatuno
	priority 5
	timeout 30
	call {
		do conf buyAuto_0_npc cmd_in01 79 182
		do iconf Erva Azul 7 1 0
		call configurarMapa "comodo" "cmd_fild02"
	}
}

automacro upAteLv54_jaTenhoErvaAzul {
	BaseLevel 46..53
	ConfigKeyNot quest_eden em_curso
	ConfigKeyNot quest_eden terminando
	ConfigKeyNot lockMap iz_dun00
	ConfigKeyNot In_saveMap_sequence true
	ConfigKeyNot virarArruaceiro true
	InInventory "Erva Azul" >= 7
	JobID 6 #gatuno
	priority 5
	timeout 30
	exclusive 1
	call {
		do conf buyAuto_0_npc izlude_in 115 61
		do mconf Cogumelo Negro 0 0 0
		do mconf Hidra -1 0 0
		call configurarMapa "izlude" "iz_dun00"
	}
}

automacro upAteLv60 {
	ConfigKeyNot quest_eden em_curso
	ConfigKeyNot quest_eden terminando
	ConfigKeyNot lockMap iz_dun02
	ConfigKeyNot In_saveMap_sequence true
	ConfigKeyNot virarArruaceiro true
	JobID 6, 17
	BaseLevel 54..59
	exclusive 1
	priority 5
	timeout 30
	call {
		do mconf Hidra -1 0 0
		do conf buyAuto_0_npc izlude_in 115 61
		call configurarMapa "izlude" "iz_dun02"
	}
}

automacro upAteLv71 {
	BaseLevel 60..70
	ConfigKeyNot quest_eden em_curso
	ConfigKeyNot quest_eden terminando
	ConfigKeyNot lockMap moc_fild17
	ConfigKeyNot In_saveMap_sequence true
	JobID 17
	priority 5
	timeout 30
	call {
		do conf buyAuto_0_npc moc_ruins 114 63
		call configurarMapa "morocc" "moc_fild17"
	}
}

automacro upAteLv80 {
	BaseLevel 71..79
	ConfigKeyNot quest_eden em_curso
	ConfigKeyNot quest_eden terminando
	ConfigKeyNot lockMap mjolnir_11
	ConfigKeyNot In_saveMap_sequence true
	JobID 17
	exclusive 1
	priority 5
	timeout 30
	call {
		do conf buyAuto_0_npc prt_in 126 76
		call configurarMapa "prontera" "mjolnir_11"
	}
}

automacro upAteLv99 {
	BaseLevel 80..98
	ConfigKeyNot quest_eden em_curso
	ConfigKeyNot quest_eden terminando
	ConfigKeyNot lockMap gef_fild06
	ConfigKeyNot In_saveMap_sequence true
	JobID 17
	exclusive 1
	priority 5
	timeout 30
	call configurarMapa "geffen" "gef_fild06"
}

automacro estouLv99 {
	BaseLevel = 99
	exclusive 1
	timeout 120
	JobID 17 #arruaceiro
	call {
		log CHEGUEI NO LVL 99 FINALMENTE !!!!!!!
		log CARA ISSO LEVOU TEMPO PARA CAR**HO
	}
}

macro configurarMapa {
	if ( &config(saveMap) = $.param[0] ) {
		do conf lockMap $.param[1]
		call voltarAtacar
	} else {
		if (&defined($.param[0])) {
			log cidade pra salvar  é : $.param[0]
		} else {
			log valor de param é $.param[0]
			log por algum motivo a cidade não ta definida... sem isso nao adianta prosseguir
			do reload event
			stop
		}
		$cidade = $.param[0]
		call SetSaveIn "$cidade"
	}
}

#
#
#
# FIM DO UP ATE O 99
#
